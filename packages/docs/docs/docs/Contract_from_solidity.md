---
id: solidity_contract
title: Creating contracts using a solidity contract
---

# Create Contracts using Solidity

ZeppelinOS helps you to create upgradable contracts, but there can be a case where you need to create an upgradable contract using a Solidity contract. 

In this guide, we will learn how to create an upgradable contract using another contract. We will take an example of `Factory` and `Instance` contract, where `Factory` is just a contract which will be used to create another `Instance` contract. 

### **Create Factory and Instance contract**

Before starting we need to [set up](https://docs.zeppelinos.org/docs/first.html#setting-up-your-project) our project using 

```console
zos init 
```

Now, we need to add a `Factory` contract and an `Instance` contract in our contracts folder. 

**Factory.sol**

```console solidity
pragma solidity ^0.5.0;

import "zos-lib/contracts/Initializable.sol";
import "zos-lib/contracts/application/App.sol";

contract Factory is Initializable {
  App public app;
  
  function initialize(address _appContractAddress) public initializer {
    app = App(_appContractAddress);
  }

  function createInstance(bytes memory _data) public returns (address proxy) {
    string memory packageName = "creating-instances-from-solidity";
    string memory contractName = "Instance";
    address admin = msg.sender;
    return address(app.create(packageName, contractName, admin, _data));
  }
}
```

**Instance.sol**

```console solidity
pragma solidity ^0.5.0;
import "zos-lib/contracts/Initializable.sol";

contract Instance is Initializable {
  uint256 public value;
  
  function initialize(uint256 _value) public initializer {
    value = _value;
  }
}
```

**Create Factory contract**

Now, we will create our `Factory` contract using `zos create` . We need to initialize our `Factory` contract with [App.sol](https://docs.zeppelinos.org/docs/architecture.html#appsol-https-githubcom-zeppelinos-zos-blob-v200-packages-lib-contracts-application-appsol) contract (provided by zos-lib) address. The `App` contract is the project’s main entry point, and it manages your whole project. It tracks the addresses of the logic contracts and corresponding names. To understand more how Zeppelin manages upgrades read [here](https://docs.zeppelinos.org/docs/architecture.html).

```console 
$ zos add

$ zos publish

$ zos create Factory

? Pick a network development
✓ Contract Factory deployed
✓ Contract Instance deployed
All contracts have been deployed
? Do you want to call a function on the instance after creating it? Yes
? Select which function * initialize(_appContractAddress: address)
? _appContractAddress (address): 0xe78A0F7E598Cc8b0Bb87894B0F60dD2a88d6a8Ab
✓ Setting everything up to create contract instances
✓ zosfactory Factory instance created at 0x9b1f7F645351AF3631a656421eD2e40f2802E6c0
0x9b1f7F645351AF3631a656421eD2e40f2802E6c0
```

Let’s unpack a few things here, `zos add` adds our `Factory` and `Instance` contract to our Zeppelin project, and `zos publish` deploys `App` contract and registers all our logic contracts (In our case `Factory` and `Instance`) on the network.

Note, while creating our Factory contract, we are also calling it’s `initialize` method and set `_appContractAddress` which we will acquire from `zos.dev-xxxx.json`. 
> Note: In `zos.dev-xxxx.json`, “xxxx” is a random number generated by zos.

**Encoding Call Data**

To create our Instance contract, We need to call it’s `initialize` method through `Factory` contract using `createInstance` method. This is our Instance contract’s initialize method.

```console solidity
  function initialize(uint256 _value) public initializer {
    value = _value;
  }
```

For that, we need to construct the call data for `createInstance` method and encode it. This call data consists of the `Instance` contract’s `initialize` method with the value of `42`. 

```console javascript
const lib = require('zos-lib');
const data = lib.encodeCall('initialize', ['uint256'], [42]);
console.log(`Call data for Instance.sol's initialize: ${data}`);
```
This will output encoded data, which we need to pass to `Factory` contract's `createInstance` method.
> Note: Output for the above script will be same for you as below `0xfe4b84df000000000000000000000000000000000000000000000000000000000000002a`

**Create Instance Contract**

Now, let's instantiate `Instance` contract using our `Factory` contract, for that we need to call `createInstance` method. We can do it using `zos send-tx`

``` console 
zos send-tx  

? Pick a network development
? Pick an instance Factory at 0x9b1f7F645351AF3631a656421eD2e40f2802E6c0
? Select which function createInstance(_data: bytes)
? _data (bytes): 0xfe4b84df000000000000000000000000000000000000000000000000000000000000002a

✓ Setting everything up to create contract instances
✓ zosfactory Instance instance created at 0x962f7A7146CA5527fB9BF92491da675F3d2De0d8
```    
> Note: We are calling `createInstance` method of `Factory` contract with our encoded data. 

**Testing Instance Contract**

Let's verify our Instance contract’s value using `zos call`.
```console 
zos call
? Pick a network development
? Pick an instance Instance at 0x962f7A7146CA5527fB9BF92491da675F3d2De0d8
? Select which function value()
✓ Method 'value()' returned: 42
```
> Note: We have passed 42 while encoding the initialize call for our instance contract.  

**Upgrade Instance Contract**

Now, we will add a `counter` variable and an increase method to our Instance contract and upgrade it.  

**Instance.sol**

```console solidity
pragma solidity ^0.5.0;
import "zos-lib/contracts/Initializable.sol";

contract Instance is Initializable {
  uint256 public value;
  uint256 public counter;
  
  function initialize(uint256 _value) public initializer {
    value = _value;
  }
  
  function increase(uint256 _counter) public {
    counter += _counter;
  }
}
```    

To upgrade the Instance contract, we need to call 
```console
zos upgrade

? Pick a network development
Nothing to compile, all contracts are up to date.
All contracts are up to date
? Which instances would you like to upgrade? Choose by name
? Pick an instance to upgrade Instance
? Do you want to call a function on the instance after upgrading it? No
Contract Instance at 0x962f7A7146CA5527fB9BF92491da675F3d2De0d8 is up to date.
```
This will upgrade our contract. 

**Wrapping UP**

Let’s summarize what we have done, we created a `Factory` contract and initialized it with `App` contract address. `App` contract tracks the addresses of the logic contracts and corresponding names. Then we created an upgradable `Instance` contract using our `Factory` contract. In the end, we upgraded our `Instance` contract. This way we learned how to create an upgradable solidity contract using another contract. 



